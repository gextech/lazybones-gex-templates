buildscript {  
  repositories {
    jcenter()
  }
  dependencies { 
    // the license plugin, see https://github.com/hierynomus/license-gradle-plugin  
    classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.7.0'
  }
}

allprojects {   
  apply plugin: 'groovy' 
  apply plugin: 'java' 
  apply plugin: 'idea'

  group = project.groupId  
  sourceCompatibility = project.javaVersion 
  targetCompatibility = project.javaVersion 
  version = project.version

   dependencies {        
     compile "org.codehaus.groovy:groovy-all:${project.groovyVersion}"
     testCompile "org.spockframework:spock-core:${project.spockVersion}"
   }

   // Disable the test report for the individual test task
   test {
     reports.html.enabled = true 
     // testLogging.showStandardStreams = true
   }

   configurations.all {
     resolutionStrategy.eachDependency { details ->
        def hystrixDeps = [
          'hystrix-core',
          'hystrix-metrics-event-stream'
        ]

        if (hystrixDeps.contains(details.requested.name)) {
          details.useVersion project.hystrixVersion
        }
     }
   }

   sourceSets { 
     main { 
       java {
         srcDirs = []
       }
       groovy {
         srcDirs += ['src/generated/groovy', 'src/generated/java', 'src/main/java']
       }
     }
     integrationTest {  
       java {
         srcDirs = []
       }
       groovy {
         srcDirs += ['src/test/integration/java', 'src/test/integration/groovy', 'src/generated/groovy', 'src/generated/java', 'src/ma/java']
       }
       resources {
         srcDir 'src/test/integration/resources'
       }
     }
   }

   task allDeps(type: DependencyReportTask) {}

   repositories {
     jcenter()
     mavenLocal()
     maven { url 'http://dl.bintray.com/gextech/oss/' }
   }

   idea {
     module {
      inheritOutputDirs = false
      outputDir = file("$buildDir/classes/main/")
      iml {
        beforeMerged { module ->
          module.dependencies.clear()
          module.excludeFolders.clear()
        }
        whenMerged { module ->
          module.dependencies*.exported = true
          module.excludeFolders += module.pathFactory.path(file(".idea"))
          module.excludeFolders += module.pathFactory.path(file(".gradle"))
          module.excludeFolders += module.pathFactory.path(file(".node_modules"))
          module.excludeFolders += module.pathFactory.path(file("$buildDir/bundles"))
          module.excludeFolders += module.pathFactory.path(file("$buildDir/classes"))
          module.excludeFolders += module.pathFactory.path(file("$buildDir/dependency-cache"))
          module.excludeFolders += module.pathFactory.path(file("$buildDir/libs"))
          module.excludeFolders += module.pathFactory.path(file("$buildDir/reports"))
          module.excludeFolders += module.pathFactory.path(file("$buildDir/test-results"))
          module.excludeFolders += module.pathFactory.path(file("$buildDir/tmp"))
          module.excludeFolders += module.pathFactory.path(file("$buildDir/matrix"))
          module.excludeFolders += module.pathFactory.path(file("$buildDir/resources"))
          module.excludeFolders -= module.pathFactory.path(file("$buildDir"))
        }
      }
      downloadSources = true
    }
  }
}

task testReport(type: TestReport) { 
  destinationDir = file("$buildDir/reports/allTests") 
  // Include the results from the `test` task in all subprojects
  reportOn subprojects*.test
}

build.dependsOn ':raml:build'

